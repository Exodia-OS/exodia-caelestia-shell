#
# Maintainer: Mahmoud Mohamed (00xWolf) <mmsaeed509@gmail.com> , <https://github.com/mmsaeed509>
#

pkgname='exodia-caelestia-shell'
_pkgname='caelestia-shell'
pkgver=1.4.1
pkgrel=6
pkgdesc='The desktop shell for the Caelestia dotfiles'
arch=('any')
url='https://github.com/Exodia-OS/exodia-caelestia-shell'
license=('GPL-3.0-only')
depends=(
    'exodia-caelestia-cli' 'quickshell-git' 'ddcutil' 'brightnessctl' 'libqalculate' 
    'app2unit' 'libcava' 'networkmanager' 'ttf-material-symbols-variable' 'swappy'
    'lm_sensors' 'fish' 'aubio' 'libpipewire' 'glibc' 'gcc-libs' 'bash' 'qt6-base'
    'power-profiles-daemon' 'ttf-rubik-vf' 'ttf-cascadia-code-nerd' 'qt6-declarative'
)
makedepends=('cmake' 'ninja')
provides=($_pkgname)
conflicts=(
    $_pkgname
    $_pkgname-git
)
source=("$url/archive/refs/tags/$pkgver.tar.gz")
sha256sums=('SKIP')
install=${pkgname}.install

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    cmake -B build -G Ninja -DCMAKE_BUILD_TYPE=Release -DCMAKE_INSTALL_PREFIX=/ -DVERSION=$pkgver
    cmake --build build
}

package() {

    cd "${srcdir}/${pkgname}-${pkgver}"
    local CONFIG_DIR=${pkgdir}/etc/skel/.config/caelestia
    mkdir -p "${CONFIG_DIR}"

    DESTDIR="$pkgdir" cmake --install build
    install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$_pkgname/LICENSE
    cp shell.json ${CONFIG_DIR}
}
